_pkgname=moodyapi-server
pkgname=$_pkgname-git
pkgver=r1500.55e3272
pkgrel=1
pkgdesc="MoodyAPI Server"
arch=('x86_64')
url="https://github.com/moodyhunter/MoodyAPI"
license=('AGPL3')
makedepends=('git' 'go' 'protobuf')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$pkgname::git+$url.git")
b2sums=('SKIP')

pkgver() {
    cd $pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
    cd $pkgname/Server
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath"
    export PATH="$PATH:$HOME/go/bin"
    go generate
    go build -ldflags='-s -w -linkmode=external'
}

package() {
    cd $pkgname
    install -Dm644 assets/$_pkgname.service "$pkgdir"/usr/lib/systemd/system/$_pkgname.service
    install -Dm755 Server/api.mooody.me "$pkgdir"/usr/bin/$_pkgname
}
