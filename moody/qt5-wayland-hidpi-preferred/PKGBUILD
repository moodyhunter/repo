# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=qt5-wayland
pkgname=${_pkgname}-hidpi-preferred
_basever=5.15.6
pkgver=5.15.7+kde+r49
pkgrel=2
_commit=8faf74a7966b520f0ac3eb4d88235f8ec63b31b8
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides APIs for Wayland'
depends=('qt5-declarative' 'libxcomposite')
makedepends=('vulkan-headers' 'git')
provides=('qt5-wayland')
conflicts=('qt5-wayland')
_pkgfqn=${_pkgname/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn #commit=$_commit
    hidpi.patch
    fix-menu-pos.patch)
sha256sums=('SKIP'
    '8e20b2b8a10916c4d5f002001f2784bd9c12b6771385e84fda71972fa081a67c'
    '3a0675890fa7fdc0287a501c6520ba9104e4ddd13d7de2c5dded1bcfcb39fd0b')
options=(debug)

prepare() {
    mkdir -p build
    cd $_pkgfqn
    patch -Np1 <$srcdir/hidpi.patch
    patch -Np1 <$srcdir/fix-menu-pos.patch
}

build() {
    cd build

    qmake ../${_pkgfqn}
    make
}

package() {
    cd build

    make INSTALL_ROOT="$pkgdir" install

    # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
    find "$pkgdir/usr/lib" -type f -name '*.prl' \
        -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

    install -d "$pkgdir"/usr/share/licenses
    ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${_pkgname}
}
