# Maintainer: Moody <mooodyhunter@outlook.com>

_target=x86_64-mos
_mos_arch=x86_64
_pkgbase=$_target-mlibc

pkgname=$_pkgbase-git
pkgver=3456.15e7213c
pkgrel=1
pkgdesc="mlibc for the $_target toolchain"
arch=('any')
url="https://github.com/moodyhunter/mlibc-mos"
license=('MIT')
provides=($_pkgbase)
conflicts=($_pkgbase)
groups=($_target-toolchain)
makedepends=(git gcc meson-stdio-tmpfix mos-api-headers-${_mos_arch} $_target-gcc)
source=("mlibc::git+https://github.com/moodyhunter/mlibc-mos.git")
sha512sums=('SKIP')
options+=(staticlibs)

pkgver() {
    cd mlibc
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd mlibc
    meson setup build \
        --prefix=$pkgdir/usr/$_target \
        -Ddefault_library=both \
        --cross-file=ci/mos-x86_64.cross-file
}

build() {
    cd mlibc/build
    ninja
}

package() {
    cd mlibc/build
    ninja install
}
