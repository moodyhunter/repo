# Maintainer: Moody Liu <mooodyhunter@outlook.com>

_pkgname=gitea-runner
pkgname=$_pkgname-git
pkgver=v0.0.1.r41.8eea12d
pkgrel=1
pkgdesc="Act runner is a runner for Gitea."
arch=('x86_64')
url="https://gitea.com/gitea/act_runner"
license=('MIT')
depends=('docker')
makedepends=('git' 'go')
provides=("$_pkgname")
conflicts=("$_pkgname")
install="PKGBUILD"
source=(
    "$_pkgname::git+https://gitea.com/gitea/act_runner"
    'gitea-runner.service'
    'gitea-runner.sysusers'
    'gitea-runner.tmpfiles'
)
noextract=()
options=(!debug)
md5sums=('SKIP'
         'f83cd53da218a9cd5e413cf129c7b408'
         '866779ce471920c6ae0c74660114524c'
         '999b8d7dc87161535ff67b1ad5b05c58')
sha512sums=('SKIP'
            '63c8e075f3a87edf209e0d1498e2e55284d1cfb69887e441a52edfdb79731973ad630db0f9f4a588eebb70b9e99083605c34da7df1767d0e5bf2664c4ad94c8a'
            'f572ecfb97ce26363418e78eb0f6614f42576b99665acc15242057e9632d255eb9be83850f55f09cc5d852da7cf0dcce5d6213afedc57cc4c65b5cbb62684175'
            '6bac7a8f76226774f05d1279585994dae33b64b1766672b000504b157d79661673c3d0cb22fa62549f6bce1a4ff3a0e7e0a42f0ecb62218e2054cbc418ac0998')

pkgver() {
    cd "$srcdir/$_pkgname"
    printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
    cd "$srcdir/$_pkgname"
    export LDFLAGS="" # this works, but why?
    make build
}

package() {
    cd "$srcdir/$_pkgname"
    install -Dm755 "$srcdir/$_pkgname/act_runner" "$pkgdir/usr/bin/act_runner"
    install -Dm644 "$srcdir/$_pkgname.service" "$pkgdir/usr/lib/systemd/system/$_pkgname.service"
    install -Dm644 "$srcdir/$_pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$_pkgname.conf"
    install -Dm644 "$srcdir/$_pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$_pkgname.conf"
    install -d -m755 "$pkgdir/var/lib/$_pkgname"
}

post_install() {
    echo
    echo "Please register runner with command:"
    echo "cd /var/lib/$_pkgname/ && sudo -u gitea-runner act_runner register --no-interactive --instance <YOUR_INSTANCE> --token <YOUR_TOKEN>"
    echo
}

pre_remove() {
    if systemctl -q is-enabled $_pkgname.service; then
        systemctl disable $_pkgname.service
    fi
}

post_remove() {
    echo
    echo "Remove $_pkgname user and this HOME /var/lib/$_pkgname manually, if not needed anymore."
    echo
}
