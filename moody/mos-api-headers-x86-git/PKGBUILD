# Maintainer: Moody <mooodyhunter@outlook.com>

_arch=x86
_target=i686-mos
_sysroot=/usr/${_target}
_pkgbase=mos-api-headers-${_arch}

pkgname=${_pkgbase}-git
pkgver=743.374a1bd
pkgrel=1
pkgdesc="MOS API headers for ${_arch}"
arch=('any')
url="https://github.com/moodyhunter/MOS"
license=('GPL3')
provides=($pkgname ${_pkgbase})
conflicts=(${_pkgbase})
groups=(mos-toolchain)
makedepends=(git cmake python)
source=("mos::git+https://github.com/moodyhunter/MOS.git")
sha512sums=('SKIP')

pkgver() {
    cd mos
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd mos
    mkdir build && cd build
    cmake .. -DMOS_ARCH=x86 -D__MOS_HAS_NO_COMPILER=ON
}

build() {
    cd mos/build
    cmake --build . --target mos_syscall_decl
}

package() {
    mkdir -p $pkgdir/${_sysroot}/include

    # generated syscall stubs
    cp -r mos/build/include/mos $pkgdir/${_sysroot}/include

    # public headers
    cp -r mos/kernel/include/public/mos $pkgdir/${_sysroot}/include

    # platform headers
    cp mos/arch/${_arch}/include/mos/platform_syscall.h $pkgdir/${_sysroot}/include/mos
}
