# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=x86_64-elf
_pkgname=binutils

pkgname=$_target-$_pkgname
pkgver=2.40
_basedir=$_pkgname-$pkgver
pkgrel=3
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib libelf)
provides=($pkgname)
groups=($_target-toolchain)
options=(!emptydirs !docs)
source=(https://mirrors.kernel.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig})
sha256sums=('0f8a4c272d7f17f369ded10a4aca28b8e304828e95526da482b0ccc4dfc9d8e1'
            'SKIP')
validpgpkeys=("3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F")

prepare() {
    cd $_basedir
    sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
    mkdir $srcdir/$_pkgname-build
}

build() {
    cd $srcdir/$_pkgname-build

    $srcdir/$_basedir/configure \
        --target=$_target \
        --with-sysroot=/usr/$_target \
        --prefix="/usr" \
        --disable-nls \
        --disable-werror

    make
}

check() {
    cd $srcdir/$_pkgname-build
    # do not abort on errors - manually check log files
    make -k check
}

package() {
    cd $srcdir/$_pkgname-build
    make DESTDIR="$pkgdir" install

    # Remove file conflicting with host binutils and manpages for MS Windows tools
    rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,windres,windmc}*
    rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so

    rm -r ${pkgdir}/usr/share/{info,man}
}
