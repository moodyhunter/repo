# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=x86_64-mos
_pkgname=binutils

pkgname=$_target-$_pkgname
pkgver=2.41
_basedir=$_pkgname-$pkgver
pkgrel=20
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib libelf)
provides=($pkgname)
groups=($_target-toolchain)
options=(!emptydirs !docs)
source=($_target-binutils.patch
    https://mirrors.kernel.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig})
sha256sums=('212810ccf019868c1b8b77f9703dcc94875bab7fa1acb2ffff74b6b431148229'
            'ae9a5789e23459e59606e6714723f2d3ffc31c03174191ef0d015bdf06007450'
            'SKIP')
validpgpkeys=("3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F")

prepare() {
    cd $srcdir/$_basedir
    sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
    patch -Np1 <$srcdir/$_target-binutils.patch
    mkdir $srcdir/$_pkgname-build
}

build() {
    cd $srcdir/$_pkgname-build

    $srcdir/$_basedir/configure \
        --target=$_target \
        --prefix="/usr/" \
        --with-sysroot="/usr/$_target" \
        --disable-nls \
        --with-pic \
        --enable-deterministic-archives \
        --disable-werror \
        --enable-gold \
        --enable-ld=default \
        --enable-thread \
        --enable-shared \
        --enable-multilib \
        --enable-plugins \
        --with-gnu-as \
        --with-gnu-ld \
        --host=${CHOST} \
        --build=${CHOST}

    make
}

check() {
    cd $srcdir/$_pkgname-build
    # do not abort on errors - manually check log files
    make -k check
}

package() {
    cd $srcdir/$_pkgname-build
    make DESTDIR="$pkgdir" install

    # Remove file conflicting with host binutils and manpages for MS Windows tools
    rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,windres,windmc}*
    rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so
    rm "$pkgdir"/usr/lib/libctf*
    rm "$pkgdir"/usr/lib/libsframe*
    rm -r "$pkgdir"/usr/include
    rm "$pkgdir"/usr/etc/gprofng.rc || true
    rm -r "$pkgdir"/usr/lib/gprofng/ || true

    # Remove info documents that conflict with host version
    rm -r "$pkgdir"/usr/share/info
}
