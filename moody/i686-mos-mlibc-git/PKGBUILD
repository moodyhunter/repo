# Maintainer: Moody <mooodyhunter@outlook.com>

_target=i686-mos
_mos_arch=x86
_pkgbase=$_target-mlibc

pkgname=$_pkgbase-git
pkgver=1.0.0
pkgrel=4
pkgdesc="mlibc for the $_target toolchain"
arch=('x86_64')
url="https://github.com/moodyhunter/mlibc-mos"
license=('MIT')
provides=($_pkgbase $_pkgbase-git)
conflicts=($_pkgbase)
groups=($_target-toolchain)
makedepends=(git gcc meson $_target-gcc mos-api-headers-${_mos_arch})
source=("mlibc::git+https://github.com/moodyhunter/mlibc-mos.git")
sha512sums=('SKIP')

pkgver() {
    cd mlibc
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd mlibc
    meson setup build \
        --prefix=$pkgdir/usr/$_target \
        --cross-file=ci/mos.cross-file \
        -Ddisable_linux_option=true \
        -Ddefault_library='static'
}

build() {
    cd mlibc/build
    ninja
}

package() {
    cd mlibc/build
    ninja install
}
