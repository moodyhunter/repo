# Maintainer: c0repwn3r <core@coredoes.dev>
# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=i686-elf

pkgname=$_target-gcc
pkgver=12.1.0
pkgrel=1
pkgdesc="GNU gcc for the $_target toolchain"
arch=(x86_64)
url="https://www.gnu.org/software/gcc"
license=('GPL')
provides=($pkgname)
makedepends=(gmp mpfr gcc)
depends=(xz libmpc i686-elf-binutils)
source=(
    "http://ftpmirror.gnu.org/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
    "gcc12-Wno-format-security.patch"
)
sha256sums=('62fd634889f31c02b64af2c468f064b47ad1ca78411c45abe6ac4b5f8dd19c7b'
    '07c72cccb31b5fb035042eca910c9bf0d3008aaeb04350534bb7f5aede209982')

build() {
    # Patch gcc
    cd "gcc-$pkgver"
    patch --strip=1 --input="$srcdir/gcc12-Wno-format-security.patch"
    cd ..

    # Create temporary build dir
    mkdir -p "$_target-$pkgname-$pkgver-build"
    cd "$_target-$pkgname-$pkgver-build"

    # Configure, we are building in seperate directory to cleanly seperate the binaries from the source
    ../gcc-$pkgver/configure \
        --target=$_target \
        --prefix="/usr" \
        --libexecdir="/usr/lib" \
        --disable-nls \
        --disable-werror \
        --disable-multilib \
        --without-headers \
        --enable-libgcc \
        --enable-languages=c,c++ \
        --disable-build-format-warnings # https://bugs.archlinux.org/task/70701

    # Build
    make all-gcc
    make all-target-libgcc
}

package() {
    cd "$_target-$pkgname-$pkgver-build"
    make install-gcc DESTDIR=$pkgdir
    make install-target-libgcc DESTDIR=$pkgdir

    # Remove conflicting files
    rm -rf $pkgdir/usr/share/info
    rm -rf $pkgdir/usr/share/man/man7
}
