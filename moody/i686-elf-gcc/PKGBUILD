# Maintainer: c0repwn3r <core@coredoes.dev>
# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=i686-elf

pkgname=$_target-gcc
pkgver=13.1.0
pkgrel=2
pkgdesc="GNU gcc for the $_target toolchain"
arch=('x86_64')
url="https://www.gnu.org/software/gcc"
license=('GPL')
provides=($pkgname)
groups=($_target-toolchain)
makedepends=(gmp mpfr gcc)
depends=(xz libmpc $_target-binutils)
options+=(!lto)
source=(
    "http://ftpmirror.gnu.org/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
)
sha256sums=('61d684f0aa5e76ac6585ad8898a2427aade8979ed5e7f85492286c4dfc13ee86')

build() {
    # Create temporary build dir
    mkdir -p "$_target-$pkgname-$pkgver-build"
    cd "$_target-$pkgname-$pkgver-build"

    # Credits @allanmcrae
    # https://github.com/allanmcrae/toolchain/blob/f18604d70c5933c31b51a320978711e4e6791cf1/gcc/PKGBUILD
    # TODO: properly deal with the build issues resulting from this
    CFLAGS=${CFLAGS/-Werror=format-security/}
    CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

    # Configure, we are building in seperate directory to cleanly seperate the binaries from the source
    ../gcc-$pkgver/configure \
        --target=$_target \
        --prefix="/usr" \
        --libexecdir="/usr/lib" \
        --disable-nls \
        --disable-werror \
        --disable-multilib \
        --without-headers \
        --enable-libgcc \
        --enable-languages=c,c++ \
        --disable-build-format-warnings # https://bugs.archlinux.org/task/70701

    # Build
    make all-gcc
    make all-target-libgcc
}

package() {
    cd "$_target-$pkgname-$pkgver-build"
    make install-gcc DESTDIR=$pkgdir
    make install-target-libgcc DESTDIR=$pkgdir

    # Remove conflicting files
    rm -rf $pkgdir/usr/share/info
    rm -rf $pkgdir/usr/share/man/man7
}
