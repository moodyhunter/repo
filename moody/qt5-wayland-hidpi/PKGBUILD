# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=qt5-wayland
pkgname=${_pkgname}-hidpi
_basever=5.15.8
pkgver=5.15.16+kde+r60
pkgrel=1
_commit=61db28f8735290acf70c124eaaccc9e6553e1cda
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides APIs for Wayland'
depends=('qt5-declarative' 'libxcomposite')
makedepends=('vulkan-headers' 'git')
provides=('qt5-wayland')
conflicts=('qt5-wayland')
_pkgfqn=${_pkgname/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
    hidpi.patch)
sha256sums=('2cb0a792af91c8612e23be9b8b8429f66bd01afe588d857bea9c9c5aa350c212'
            '8e20b2b8a10916c4d5f002001f2784bd9c12b6771385e84fda71972fa081a67c')
options=(debug)

prepare() {
    mkdir -p build
    cd $_pkgfqn
    patch -Np1 <$srcdir/hidpi.patch
}

build() {
    cd build

    qmake ../${_pkgfqn}
    make
}

package() {
    cd build

    make INSTALL_ROOT="$pkgdir" install

    # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
    find "$pkgdir/usr/lib" -type f -name '*.prl' \
        -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

    install -d "$pkgdir"/usr/share/licenses
    ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${_pkgname}
}
