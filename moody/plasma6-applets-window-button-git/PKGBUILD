pkgname=plasma6-applets-window-buttons6-git
pkgver=r193.c787f66
pkgrel=1
url='https://github.com/moodyhunter/applet-window-buttons6'
arch=(x86_64)
pkgdesc='Plasma applet in order to show window buttons in your panels'
license=(GPL-2.0-or-later LGPL-2.1-or-later)
depends=(gcc-libs
  glibc
  kcolorscheme
  kconfig
  kcoreaddons
  kdecoration
  ki18n
  kirigami
  kitemmodels
  ksvg
  libplasma
  plasma-workspace
  qt6-base
  qt6-declarative)
makedepends=(git extra-cmake-modules)
source=(git+https://github.com/moodyhunter/applet-window-buttons6.git)
sha256sums=('SKIP')

pkgver() {
  cd "applet-window-buttons6"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cmake -B build -S applet-window-buttons6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
