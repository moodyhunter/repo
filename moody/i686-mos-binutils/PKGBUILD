# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=i686-mos
_pkgname=binutils

pkgname=$_target-$_pkgname
pkgver=2.40
_basedir=$_pkgname-$pkgver
pkgrel=9
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib libelf $_target-mlibc)
provides=($pkgname)
groups=($_target-toolchain)
options=(!emptydirs !docs)
source=(i686-mos-binutils.patch
    https://mirrors.kernel.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig})
sha256sums=('fd89c57e76deb9cd085c06a1617b1868cf75a9fe7358722052596dce4f0d2814'
            '0f8a4c272d7f17f369ded10a4aca28b8e304828e95526da482b0ccc4dfc9d8e1'
            'SKIP')
validpgpkeys=("3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F")

prepare() {
    cd $srcdir/$_basedir
    sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
    patch -Np1 <$srcdir/i686-mos-binutils.patch
    mkdir $srcdir/$_pkgname-build
}

build() {
    cd $srcdir/$_pkgname-build

    $srcdir/$_basedir/configure \
        --target=$_target \
        --prefix="/usr/" \
        --with-sysroot="/usr/$_target" \
        --disable-nls \
        --with-pic \
        --enable-deterministic-archives \
        --disable-werror \
        --enable-gold \
        --enable-ld=default \
        --enable-thread \
        --enable-multilib \
        --enable-plugins \
        --with-gnu-as \
        --with-gnu-ld \
        --host=${CHOST} \
        --build=${CHOST}

    make
}

check() {
    cd $srcdir/$_pkgname-build
    # do not abort on errors - manually check log files
    make -k check
}

package() {
    cd $srcdir/$_pkgname-build
    make DESTDIR="$pkgdir" install
    # Remove info documents that conflict with host version
    rm -r "$pkgdir/usr/share/info"
    rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so
}
