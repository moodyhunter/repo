# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=i686-mos
_pkgname=binutils

pkgname=$_target-$_pkgname
pkgver=2.39
_basedir=$_pkgname-$pkgver
pkgrel=5
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=(x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib libelf)
provides=($pkgname)
groups=($_target-toolchain)
options=(!emptydirs !docs)
source=(i686-mos-binutils.patch
            https://mirrors.kernel.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig})
sha256sums=('01c18b3204601ac86d4ed47c0b8bc27455e7095235fb65212a54d7b8ccffb025'
            '645c25f563b8adc0a81dbd6a41cffbf4d37083a382e02d5d3df4f65c09516d00'
            'SKIP')
validpgpkeys=("3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F")

prepare() {
  cd $srcdir/$_basedir
  sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
  patch -Np1 < $srcdir/i686-mos-binutils.patch
  mkdir $srcdir/$_pkgname-build
}

build() {
  cd $srcdir/$_pkgname-build

  $srcdir/$_basedir/configure \
    --target=$_target \
    --with-sysroot=/opt/i686-mos-dev \
    --prefix="/usr" \
    --disable-nls \
    --disable-werror

  make
}

check() {
  cd $srcdir/$_pkgname-build
  # do not abort on errors - manually check log files
  make -k check
}

package() {
  cd $srcdir/$_pkgname-build
  make DESTDIR="$pkgdir" install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,windres,windmc}*
  rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so

  rm -r ${pkgdir}/usr/share/{info,man}
}
