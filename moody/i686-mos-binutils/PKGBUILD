# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=i686-mos
_pkgname=binutils

pkgname=$_target-$_pkgname
pkgver=2.40
_basedir=$_pkgname-$pkgver
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files for the $_target target"
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib libelf)
provides=($pkgname)
groups=($_target-toolchain)
options=(!emptydirs !docs)
source=(i686-mos-binutils.patch
    https://mirrors.kernel.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig})
sha256sums=('9d9138fd4f67090bb9482f578a38f0361caab067487b8a952c1d8fe6e80d32a9'
            '0f8a4c272d7f17f369ded10a4aca28b8e304828e95526da482b0ccc4dfc9d8e1'
            'SKIP')
validpgpkeys=("3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F")

prepare() {
    cd $srcdir/$_basedir
    sed -i '/ac_cpp=/s/$CPPFLAGS/$CPPFLAGS -O2/' libiberty/configure
    patch -Np1 <$srcdir/i686-mos-binutils.patch
    mkdir $srcdir/$_pkgname-build
}

build() {
    cd $srcdir/$_pkgname-build

    $srcdir/$_basedir/configure \
        --target=$_target \
        --prefix="/opt/i686-mos/" \
        --disable-nls \
        --disable-werror

    make
}

check() {
    cd $srcdir/$_pkgname-build
    # do not abort on errors - manually check log files
    make -k check
}

package() {
    cd $srcdir/$_pkgname-build
    make DESTDIR="$pkgdir" install
}
