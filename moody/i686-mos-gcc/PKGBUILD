# Maintainer: c0repwn3r <core@coredoes.dev>
# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=i686-mos
_sysroot=/usr/$_target

pkgname=$_target-gcc
pkgver=12.2.0
pkgrel=10
pkgdesc="GNU gcc for the $_target toolchain"
arch=('x86_64')
url="https://www.gnu.org/software/gcc"
license=('GPL')
provides=($pkgname)
groups=($_target-toolchain)
makedepends=(gmp mpfr gcc $_target-mlibc)
depends=(xz libmpc $_target-binutils)
options+=(!lto)
source=(
    "http://ftpmirror.gnu.org/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
    "gcc12-Wno-format-security.patch"
    "i686-mos-gcc.patch"
)
sha256sums=('e549cf9cf3594a00e27b6589d4322d70e0720cdd213f39beb4181e06926230ff'
            '07c72cccb31b5fb035042eca910c9bf0d3008aaeb04350534bb7f5aede209982'
            '45c9ac11c19a4a301db6d72eaafabf1faab88e0d6505c125e3497d8b162d1598')

prepare() {
    # Patch gcc
    cd "gcc-$pkgver"
    patch --strip=1 --input="$srcdir/gcc12-Wno-format-security.patch"
    patch -Np1 < "$srcdir/i686-mos-gcc.patch"
}

build() {
    # Create temporary build dir
    mkdir -p "$_target-$pkgname-$pkgver-build"
    cd "$_target-$pkgname-$pkgver-build"

    # Configure, we are building in seperate directory to cleanly seperate the binaries from the source
    ../gcc-$pkgver/configure \
        --target=$_target \
        --prefix="/usr" \
        --libdir="/usr/lib" \
        --with-sysroot="${_sysroot}" \
        --disable-werror \
        --disable-nls \
        --enable-shared \
        --disable-build-format-warnings

    # Build
    make
}

package() {
    cd "$_target-$pkgname-$pkgver-build"
    make install DESTDIR=$pkgdir

    # Strip target binaries
    find "$pkgdir/usr/lib/gcc/$_target/" "$pkgdir/usr/$_target/lib" -type f \
        -and \( -name \*.a -or -name \*.o \) -exec $_target-objcopy \
        -R .comment -R .note -R .debug_info -R .debug_aranges -R .debug_pubnames \
        -R .debug_pubtypes -R .debug_abbrev -R .debug_line -R .debug_str \
        -R .debug_ranges -R .debug_loc '{}' \;

    # Strip host binaries
    find "$pkgdir/usr/bin/" "$pkgdir/usr/lib/gcc/$_target/" -type f \
        -and \( -executable \) -exec strip '{}' \;

    rm -rf "$pkgdir/usr/share"
    rm -rf "$pkgdir/usr/include"

    # Remove files that conflict with host gcc package
    rm -r "$pkgdir/usr/share/"{man/man7,info,"gcc-$pkgver"}
}
