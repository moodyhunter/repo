# Maintainer: c0repwn3r <core@coredoes.dev>
# Maintainer: Finlay Maroney <finman292004@protonmail.com>

_target=i686-mos
_sysroot=/usr/$_target

pkgname=$_target-gcc
pkgver=12.2.0
pkgrel=33
pkgdesc="GNU gcc for the $_target toolchain"
arch=('x86_64')
url="https://www.gnu.org/software/gcc"
license=('GPL')
provides=($pkgname)
groups=($_target-toolchain)
makedepends=(gmp mpfr gcc $_target-mlibc)
depends=(xz libmpc $_target-binutils)
options+=(!lto !debug staticlibs)
source=(
    "http://ftpmirror.gnu.org/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
    "i686-mos-gcc.patch"
)
sha256sums=('e549cf9cf3594a00e27b6589d4322d70e0720cdd213f39beb4181e06926230ff'
            '733ff52ac69ab8964c758d6ca5e3bdc2483ee2db0078eaec06e90372b7704eec')

prepare() {
    # Patch gcc
    cd "gcc-$pkgver"
    patch -Np1 <"$srcdir/i686-mos-gcc.patch"
}

build() {
    # Create temporary build dir
    mkdir -p "$_target-$pkgname-$pkgver-build"
    cd "$_target-$pkgname-$pkgver-build"

    # Credits @allanmcrae
    # https://github.com/allanmcrae/toolchain/blob/f18604d70c5933c31b51a320978711e4e6791cf1/gcc/PKGBUILD
    # TODO: properly deal with the build issues resulting from this
    CFLAGS=${CFLAGS/-Werror=format-security/}
    CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

    # Configure, we are building in seperate directory to cleanly seperate the binaries from the source
    ../gcc-$pkgver/configure \
        --target=$_target \
        --prefix="/usr" \
        --libdir="/usr/lib" \
        --libexecdir="/usr/libexec" \
        --with-sysroot="${_sysroot}" \
        --disable-werror \
        --disable-nls \
        --with-native-system-header-dir="/include" \
        --with-build-sysroot="${_sysroot}" \
        --enable-shared \
        --disable-build-format-warnings \
        --enable-languages=c,c++ \
        --enable-__cxa_atexit \
        --enable-linker-build-id \
        --enable-lto \
        --enable-plugin

    # Build
    make
}

package() {
    cd "$_target-$pkgname-$pkgver-build"

    # https://github.com/archlinux/svntogit-community/blob/packages/aarch64-linux-gnu-gcc/trunk/PKGBUILD
    make DESTDIR="$pkgdir" install-gcc install-target-{libgcc,libstdc++-v3,libgomp,libgfortran,libquadmath,libatomic}

    # strip target binaries
    find "$pkgdir"/usr/lib/gcc/$_target/ "$pkgdir"/usr/$_target/lib \
        -type f -and \( -name \*.a -or -name \*.o \) \
        -exec $_target-objcopy -R .comment -R .note -R .debug_info -R .debug_aranges \
        -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line \
        -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

    # strip host binaries
    find "$pkgdir"/usr/bin/ "$pkgdir"/usr/lib/gcc/$_target/ -type f -and \( -executable \) -exec strip '{}' \;

    # Remove files that conflict with host gcc package
    rm -r "$pkgdir"/usr/share/man/man7
    rm -r "$pkgdir"/usr/share/info
    rm -r "$pkgdir"/usr/share/gcc-$pkgver
}
